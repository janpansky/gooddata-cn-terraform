###
# Provision EKS cluster
###

# Allow nodes to create repositories (the first time an image is pulled through the cache)
resource "aws_iam_policy" "ecr_pull_through_cache_min" {
  count = var.enable_image_cache ? 1 : 0

  name        = "${var.deployment_name}-ECRPullThroughCacheMin"
  description = "Allow worker nodes to create ECR repositories and import upstream images via pull-through cache."

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "ecr:CreateRepository",
          "ecr:BatchImportUpstreamImage"
        ],
        Resource = "*"
      }
    ]
  })
}

locals {
  ecr_pull_through_cache_policy = var.enable_image_cache && length(aws_iam_policy.ecr_pull_through_cache_min) > 0 ? {
    ECRPullThroughCacheMin = aws_iam_policy.ecr_pull_through_cache_min[0].arn
  } : {}

  eks_node_type_presets = {
    dev        = ["m6a.xlarge", "m6a.2xlarge"]
    prod-small = ["m8a.xlarge", "m8a.2xlarge"]
    prod-large = ["m8a.xlarge", "m8a.2xlarge", "m8a.4xlarge"]
    prod-xl    = ["m8a.xlarge", "m8a.2xlarge", "m8a.4xlarge"]
  }

  eks_starrocks_node_type_presets = {
    dev        = ["r8a.large", "m8a.xlarge"]
    prod-small = ["r8a.large", "r8a.xlarge"]
    prod-xl    = ["r8a.large", "r8a.8xlarge"]
  }

  # There is no dedicated prod-large StarRocks profile; fall back to prod-xl
  # StarRocks sizing when size_profile is prod-large (see gdcn-size-prod-large).
  starrocks_size_profile_effective = coalesce(var.starrocks_size_profile, var.size_profile == "prod-large" ? "prod-xl" : var.size_profile)

  eks_node_types           = coalesce(var.eks_node_types, local.eks_node_type_presets[var.size_profile])
  eks_starrocks_node_types = coalesce(var.eks_starrocks_node_types, local.eks_starrocks_node_type_presets[local.starrocks_size_profile_effective])

  # Per-AZ node groups for StarRocks so the cluster autoscaler can scale
  # nodes in the AZ where the FE/CN EBS volume lives (EBS is zonal).
  # Keyed by subnet index (known at plan time) — subnet IDs from a freshly
  # created VPC are unknown until apply, so they cannot appear in map keys
  # or in for_each sets.
  starrocks_ng_pairs = var.enable_ai_lake ? {
    for pair in setproduct(local.eks_starrocks_node_types, range(length(local.private_subnet_ids))) :
    "sr-${replace(pair[0], ".", "-")}-az${pair[1]}" => {
      instance_type = pair[0]
      subnet_id     = local.private_subnet_ids[pair[1]]
      az            = data.aws_subnet.private[tostring(pair[1])].availability_zone
    }
  } : {}
}

data "aws_subnet" "private" {
  for_each = var.enable_ai_lake ? { for idx, id in local.private_subnet_ids : idx => id } : {}
  id       = each.value
}

module "eks" {
  source  = "terraform-aws-modules/eks/aws"
  version = "~> 21.0"

  name                         = var.deployment_name
  kubernetes_version           = var.eks_version
  endpoint_public_access       = var.eks_endpoint_public_access
  endpoint_private_access      = var.eks_endpoint_private_access
  endpoint_public_access_cidrs = var.eks_endpoint_public_access_cidrs

  tags = local.common_tags

  addons = {
    coredns = {
      resolve_conflicts_on_create = "OVERWRITE"
      resolve_conflicts_on_update = "OVERWRITE"
    }
    eks-pod-identity-agent = {}
    kube-proxy             = {}
    vpc-cni = {
      before_compute = true
    }
    aws-ebs-csi-driver = {
      resolve_conflicts_on_create = "OVERWRITE"
      resolve_conflicts_on_update = "OVERWRITE"
    }
  }

  # Adds the current caller identity as an administrator via cluster access entry
  enable_cluster_creator_admin_permissions = true

  vpc_id     = local.vpc_id
  subnet_ids = local.private_subnet_ids

  # One node group per instance type so the cluster autoscaler can
  # independently evaluate and scale each size (least-waste expander).
  # StarRocks gets a dedicated taint+label pool so FE/CN pods are isolated
  # from the shared workload pool.
  eks_managed_node_groups = merge(
    {
      for instance_type in local.eks_node_types : replace(instance_type, ".", "-") => {
        create                     = true
        ami_type                   = "BOTTLEROCKET_x86_64"
        instance_types             = [instance_type]
        use_custom_launch_template = false
        disk_size                  = 100

        tags = merge(
          local.common_tags,
          {
            "k8s.io/cluster-autoscaler/enabled"                = "true"
            "k8s.io/cluster-autoscaler/${var.deployment_name}" = "owned"
          }
        )

        iam_role_additional_policies = merge({
          AmazonEBSCSIDriverPolicy           = "arn:aws:iam::aws:policy/service-role/AmazonEBSCSIDriverPolicy"
          AmazonEC2ContainerRegistryPullOnly = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryPullOnly"
        }, local.ecr_pull_through_cache_policy)

        min_size = 0
        max_size = var.eks_max_nodes

        # This value is ignored after the initial creation
        # https://github.com/bryantbiggs/eks-desired-size-hack
        desired_size = instance_type == local.eks_node_types[0] ? 1 : 0
      }
    },
    {
      for ng_name, ng in local.starrocks_ng_pairs : ng_name => {
        create                     = true
        ami_type                   = "BOTTLEROCKET_x86_64"
        instance_types             = [ng.instance_type]
        use_custom_launch_template = false
        disk_size                  = 100
        subnet_ids                 = [ng.subnet_id]

        labels = {
          workload = "starrocks"
        }
        taints = {
          starrocks = {
            key    = "workload"
            value  = "starrocks"
            effect = "NO_SCHEDULE"
          }
        }

        tags = merge(
          local.common_tags,
          {
            "k8s.io/cluster-autoscaler/enabled"                                         = "true"
            "k8s.io/cluster-autoscaler/${var.deployment_name}"                          = "owned"
            "k8s.io/cluster-autoscaler/node-template/label/workload"                    = "starrocks"
            "k8s.io/cluster-autoscaler/node-template/label/topology.kubernetes.io/zone" = ng.az
            "k8s.io/cluster-autoscaler/node-template/taint/workload"                    = "starrocks:NoSchedule"
          }
        )

        iam_role_additional_policies = merge({
          AmazonEBSCSIDriverPolicy           = "arn:aws:iam::aws:policy/service-role/AmazonEBSCSIDriverPolicy"
          AmazonEC2ContainerRegistryPullOnly = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryPullOnly"
        }, local.ecr_pull_through_cache_policy)

        min_size     = 0
        max_size     = var.eks_max_nodes
        desired_size = 0
      }
    },
    # Optional GPU pool for self-hosted LLM inference (vLLM, SIE). Bottlerocket
    # NVIDIA variant ships the NVIDIA device plugin; the taint keeps general
    # workloads off the expensive nodes.
    var.enable_inference_gpu_pool ? {
      inference-gpu = {
        create                     = true
        ami_type       = "BOTTLEROCKET_x86_64_NVIDIA"
        instance_types = concat([var.inference_gpu_instance_type], var.inference_gpu_additional_instance_types)
        capacity_type  = "ON_DEMAND"

        # This pool needs a custom launch template (block_device_mappings below),
        # and with a custom LT EKS does NOT auto-attach the cluster primary
        # security group the way it does for the default (non-custom-LT) node
        # groups. Without it the GPU nodes only carry the module node SG, their
        # pods can't reach CoreDNS/NATS (cross-node pod traffic is dropped), and
        # DNS times out cluster-wide on these nodes. Attach it explicitly.
        attach_cluster_primary_security_group = true

        # Custom LT also defaults the IMDS hop limit to 1, which stops pods (one
        # network hop from the node) from reaching IMDS — so pods relying on the
        # node IAM role for AWS creds get NoCredentialsError (e.g. the SIE worker
        # pulling model weights from the S3 cluster cache). EKS's default LT uses
        # 2; set it explicitly here.
        metadata_options = {
          http_endpoint               = "enabled"
          http_tokens                 = "required"
          http_put_response_hop_limit = 2
        }

        # Bottlerocket has two block devices:
        #   /dev/xvda (4 GB)  — read-only OS root; disk_size would resize this but it's useless
        #   /dev/xvdb (18 GB) — writable data volume: container images, model weights, etc.
        # We must explicitly resize xvdb; disk_size alone only touches xvda.
        # NVIDIA container images + model weights easily exceed the 18 GB AMI default.
        block_device_mappings = {
          xvdb = {
            device_name = "/dev/xvdb"
            ebs = {
              volume_size           = 300
              volume_type           = "gp3"
              delete_on_termination = true
            }
          }
        }

        # Span ALL AZs (1a/1b default private + 1c/1d inference overflow) so the
        # cluster-autoscaler can place GPU nodes wherever g6e capacity exists.
        # Single-AZ pinning is unworkable here: g6e.4xlarge on-demand capacity is
        # spotty (~1 node per AZ), so two nodes can't come from one AZ. Neither
        # workload needs a fixed AZ: SIE uses an emptyDir model cache, and vLLM's
        # cache PVC is gp3/WaitForFirstConsumer so it binds in whatever AZ the
        # pod's node lands. (Trade-off vs the old pin: after a vLLM scale-to-zero
        # its bound PVC re-locks it to that AZ on unpark — handle at unpark time.)
        subnet_ids = concat(
          local.private_subnet_ids,
          [for s in aws_subnet.inference_private : s.id],
        )

        labels = {
          workload = "inference"
        }
        taints = {
          inference = {
            key    = "workload"
            value  = "inference"
            effect = "NO_SCHEDULE"
          }
        }

        tags = merge(
          local.common_tags,
          {
            "k8s.io/cluster-autoscaler/enabled"                      = "true"
            "k8s.io/cluster-autoscaler/${var.deployment_name}"       = "owned"
            "k8s.io/cluster-autoscaler/node-template/label/workload" = "inference"
            "k8s.io/cluster-autoscaler/node-template/taint/workload" = "inference:NoSchedule"
          }
        )

        iam_role_additional_policies = merge({
          AmazonEBSCSIDriverPolicy           = "arn:aws:iam::aws:policy/service-role/AmazonEBSCSIDriverPolicy"
          AmazonEC2ContainerRegistryPullOnly = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryPullOnly"
          ModelCacheAccess                   = aws_iam_policy.model_cache_access[0].arn
        }, local.ecr_pull_through_cache_policy)

        # Scale-from-zero: the node only comes up when an inference pod is
        # scheduled (autoscaler reads the node-template tags above) and is
        # removed ~10 min after the last pod is gone. GPU cost accrues only
        # while something is actually running.
        min_size     = 0
        max_size     = var.inference_gpu_max_nodes
        desired_size = 0
      }
    } : {},
  )

  node_security_group_additional_rules = var.ingress_controller == "istio_gateway" ? {
    istio_xds = {
      description                   = "Istio XDS (istiod) to workloads"
      protocol                      = "tcp"
      from_port                     = 15012
      to_port                       = 15012
      type                          = "ingress"
      source_cluster_security_group = true
    }
    istio_webhook = {
      description                   = "Istio webhook/istiod"
      protocol                      = "tcp"
      from_port                     = 15017
      to_port                       = 15017
      type                          = "ingress"
      source_cluster_security_group = true
    }
  } : {}
}

# Outputs
output "eks_cluster_name" {
  description = "Name of the EKS cluster"
  value       = module.eks.cluster_name
}
